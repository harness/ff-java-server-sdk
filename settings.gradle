rootProject.name = "ff-java-server-sdk"

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // main sdk version
            version('sdk', '1.9.3');

            // sdk deps
            version('okhttp3', '4.12.0')
            library('okhttp3-core', 'com.squareup.okhttp3', 'okhttp').versionRef('okhttp3')
            library('okhttp3-logging-interceptor', 'com.squareup.okhttp3', 'logging-interceptor').versionRef('okhttp3')
            library('gson-fire', 'io.gsonfire:gson-fire:1.7.1')
            library('google-gson', 'com.google.code.gson:gson:2.10')
            library('caffeine-cache', 'com.github.ben-manes.caffeine:caffeine:2.8.5')
            library('murmur3', 'com.sangupta:murmur:1.0.0')
            library('swagger-annotations', 'io.swagger:swagger-annotations:1.6.2')
            library('javax-annotation-api', 'javax.annotation:javax.annotation-api:1.2')
            library('slf4j-api', 'org.slf4j:slf4j-api:1.7.36')
            library('lombok', 'org.projectlombok:lombok:1.18.38')
            library('google-findbugs', 'com.google.code.findbugs:jsr305:3.0.2')

            // test libs
            library('logback-classic', 'ch.qos.logback:logback-classic:1.3.14')
            library('junit-platform-launcher', 'org.junit.platform:junit-platform-launcher:1.10.1')
            library('junit-bom', 'org.junit:junit-bom:5.10.1')
            library('mockito-junit5', 'org.mockito:mockito-junit-jupiter:4.8.1')
            library('okhttp3-mockwebserver', 'com.squareup.okhttp3', 'mockwebserver').versionRef('okhttp3')
            version('jmh', '1.37')
            library('jmh-core', 'org.openjdk.jmh', 'jmh-core').versionRef('jmh')
            library('jmh-annotations', 'org.openjdk.jmh', 'jmh-generator-annprocess').versionRef('jmh')

            // examples
            library('bc', 'org.bouncycastle:bcpkix-jdk18on:1.78.1')
            library('harness.ff.sdk', 'io.harness', 'ff-java-server-sdk').versionRef('sdk')

            // plugins
            // do not upgrade openapi, doing so will break compatibility with customers using SpringBoot 2.5.x
            // (newer 5.x.x generators use APIs not present in okhttp 3.14.9)
            version('openapi.generator', '4.3.1')
            version('spotless', '7.2.1')
            version('depsize', '0.2.0')
            version('spotbugs', '6.2.3')
            version('depcheck', '12.1.3')
            version('maven.publish', '0.33.0')
        }
    }
}

include 'examples'
